pluginManagement {
    includeBuild("build-logic")

    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()

        // Or use a single nexus repository
        // maven {
        //     url "https://nexus.myrepo.ru/repository/"
        // }
    }
}

dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)

    repositories {
        google()
        mavenCentral()

        // Or use a single nexus repository
        // maven {
        //     url "https://nexus.myrepo.ru/repository/"
        // }
    }
}

enableFeaturePreview "STABLE_CONFIGURATION_CACHE"

boolean isCiServer = System.getenv().containsKey("CI")

buildCache {
    remote(HttpBuildCache) {
        // Activate if you have a remote cache implemented
        enabled = false

        // Add your URL
        url = 'https://example.com/cache/'

        setPush(isCiServer)

        credentials {
            username = System.getenv("REMOTE_CACHE_USERNAME")
            password = System.getenv("REMOTE_CACHE_PASSWORD")
        }
    }
}

rootProject.name = "idiomic-android-arch"
// Configuration
include 'app'
// Base
include ':product:main'
// Core
include ':product:core:component-ui'
// Design system
include ':product:designsystem:components'
include ':product:designsystem:theme'
// Features
include ':product:mvvm-google'
include ':product:mvi-kotlin'
